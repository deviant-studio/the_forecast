// Top-level build file where you can add configuration options common to all sub-projects/modules.


buildscript {
    ext.kotlinVersion = '1.0.+'
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.0.0-alpha9'
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.8'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath 'me.tatarka:gradle-retrolambda:3.3.0-beta3'
        //classpath 'com.jakeout:gradle-inspector:+'    //https://github.com/jakeouellette/inspector
    }
}

allprojects {
    repositories {
        jcenter()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
        maven { url 'http://oss.jfrog.org/artifactory/oss-snapshot-local' }

    }
    //apply plugin: 'com.jakeout.gradle-inspector'
}

task clean(type: Delete) {
    delete rootProject.buildDir
}


task showClasspath << {
    buildscript.configurations.classpath.each { println it.name }
}

gradle.useLogger(new TimestampEventLogger())

class TimestampEventLogger extends BuildAdapter implements TaskExecutionListener {

    def timestamp = 0L

    public void beforeExecute(Task task) {
        print "> $task.project.name:$task.name "
        timestamp = System.currentTimeMillis()
    }

    public void afterExecute(Task task, TaskState state) {
        def elapsed = (System.currentTimeMillis() - timestamp) / 1000F
        println "[$elapsed]"
    }

    public void buildFinished(BuildResult result) {
        println 'build completed'
        if (result.failure != null) {
            result.failure.printStackTrace()
        }
    }
}
